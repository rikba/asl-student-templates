dimensions
  n_u = 3   # inputs
  n_x = 10  # states
  n_d = 10  # constant disturbances
  n_w = 2   # wind disturbances (x,y)
  N = 40    # horizon  
end

parameters
  # dynamics matrix
  A (n_x,n_x){1,1 1,2 2,2 3,3 3,4 4,4 5,5 5,6 6,6 3,7 4,7 7,7 
              8,7 3,8 4,8 7,8 8,8 1,9 2,9 9,9 10,9 1,10 2,10 
              9,10 10,10}      
  # transfer matrix  
  B (n_x,n_u){5,1 6,1 3,2 4,2 7,2 8,2 1,3 2,3 9,3 10,3}   
  # disturbance transfer matrix  
  Bd (n_x,n_d){1,1 1,2 2,2 3,3 3,4 4,4 5,5 5,6 6,6 3,7 4,7 7,7 
               8,7 3,8 4,8 7,8 8,8 1,9 2,9 9,9 10,9 1,10 2,10 
               9,10 10,10}               
  # wind transfer matrix
  Bw (n_x,n_w){1,1 2,1 3,2 4,2}               
  Q (n_x,n_x) diagonal psd   # state cost
  Q_final (n_x,n_x) psd      # final state cost
  R (n_u,n_u) diagonal psd   # input cost
  x[0] (n_x)                 # initial state
  u_max (n_u)                # upper input constraints
  u_min (n_u)                # lower input constraints
  x_ss[t] (n_x), t=0..N      # desired state at steady state
  u_ss[t] (n_u), t=0..N-1    # desired input at steady state
  d (n_d)                    # estimated constant disturbances
  w (n_w)                    # estimated constant wind
end

variables
  x[t]  (n_x), t=1..N+1  # state
  u[t]  (n_u), t=0..N    # input
end

minimize
  sum[t=0..N](quad(x[t]-x_ss, Q) + quad(u[t] - u_ss, R)) 
  + quad(x[N+1]-x_ss, Q_final)
subject to
  # dynamics constraints
  x[t+1] == A*x[t] + B*u[t] + Bw*w + Bd*d, t=0..N  
  # upper input constaint halfspace  
  u[t] <= u_max, t=0..N            
  # lower input constraint halfspace  
  u[t] >= u_min, t=0..N                             
end
